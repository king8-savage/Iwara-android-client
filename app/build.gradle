apply plugin: 'com.android.application'
if (file('google-services.json').exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}

static def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

static def gitVersionTag(boolean isPattern) {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    if (isPattern){
        def pattern = "-(\\d+)-g"
        def matcher = version =~ pattern

        if (matcher) {
            version = version.substring(0, matcher.start()) + "." + matcher[0][1]
        } else {
            version = version + ".0"
        }
    }

    return version
}

static def time(){
    return new Date().format('yyyyMMddHHmm',TimeZone.getDefault())
}

task switchToDebug(type: Copy) {
    description = 'Switches to DEBUG google-services.json'
    from "src/debug"
    include "google-services.json"
    into "."
}

task switchToRelease(type: Copy) {
    description = 'Switches to RELEASE google-services.json'
    from "src/release"
    include "google-services.json"
    into "."
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.zhkrb.iwara"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 2
        versionName "0.0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            // 为了不和 release 版本冲突
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    applicationVariants.all{ variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.each { output ->
                output.versionNameOverride = gitVersionTag(true)
                output.versionCodeOverride = gitVersionCode()
                output.outputFileName = "iwara_${variant.buildType.name}_${defaultConfig.versionCode}_${time()}.apk"
            }
        }else if (variant.buildType.name == 'debug') {
            variant.outputs.each { output ->
                output.versionNameOverride = gitVersionTag(false)+'-debug'
                output.versionCodeOverride = gitVersionCode()
            }
        }
    }

    afterEvaluate {
        if (file('google-services.json').exists()) {
            processDebugGoogleServices.dependsOn switchToDebug
            processReleaseGoogleServices.dependsOn switchToRelease
        }
    }

}

repositories {
    flatDir {
        dirs 'libs', '../libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation('com.android.support:appcompat-v7:28.0.0')
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.android.material:material:1.3.0-alpha03'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:swiperefreshlayout:28.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'

    implementation 'com.alibaba:fastjson:1.2.58'

    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'


    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.7'


    implementation 'org.jsoup:jsoup:1.12.1'
    api project(':dragvideo')
    api project(':dialog')

    // Firebase
    implementation 'com.google.firebase:firebase-core:18.0.0'
    implementation 'com.google.firebase:firebase-analytics:18.0.0'

    implementation 'com.google.firebase:firebase-crashlytics:17.3.0'

    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'

    implementation 'com.zhkrb.cloudflare-scrape-android:scrape-webview:0.0.3'

    //refresh
    implementation  'com.scwang.smart:refresh-layout-kernel:2.0.1'
    implementation  'com.scwang.smart:refresh-header-classics:2.0.1'
}
